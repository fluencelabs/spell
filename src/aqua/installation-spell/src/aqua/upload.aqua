import "./config.aqua"
import IpfsClient from "./files.aqua"
import CID, JsonString, Path, Multiaddr, Address from "./types.aqua"
import SpellConfigJson, JsonDefault from "./json.aqua"
import Console from "./console.aqua"

func upload_worker_config(ipfs: Multiaddr, config: LocalWorkerConfig) -> CID:
    services: *Service
    spells: *Spell
    for s <- config.services:
        modules: *Module
        for m <- s.modules:
            wasm <- IpfsClient.upload(ipfs, m.wasm)
            cfg <- IpfsClient.upload_string(ipfs, m.config)
            modules <<- Module(wasm = wasm, config = cfg)
        services <<- Service(name = s.name, modules = modules)

    for sp <- config.spells:
        script <- IpfsClient.upload_string(ipfs, sp.script)
        cfg_str <- SpellConfigJson.stringify(sp.config)
        cfg <- IpfsClient.upload_string(ipfs, cfg_str)
        init_args_str <- JsonDefault.stringify(sp.init_args)
        init_args <- IpfsClient.upload_string(ipfs, init_args_str)
        spells <<- Spell(name = sp.name, script = script, config = cfg, init_args = init_args)

    rac = WorkerDefinition(services = services, spells = spells)
    json <- WDJson.stringify(rac)
    cid <- IpfsClient.upload_string(ipfs, json)
    <- cid

func upload(config: LocalAppDeployConfig) -> AppDeployDefinition:
    deploy_defs: *WorkerDeployDefinition
    for w <- config.workers:
        definition_cid <- upload_worker_config(IPFS, w.config)
        deploy_defs <<- WorkerDeployDefinition(
            name = w.name,
            hosts = w.hosts,
            definition = definition_cid
        )

    deploy_definition = AppDeployDefinition(
        installation_script = config.installation_script,
        installation_trigger = config.installation_trigger,
        workers = deploy_defs
    )
    <- deploy_definition
